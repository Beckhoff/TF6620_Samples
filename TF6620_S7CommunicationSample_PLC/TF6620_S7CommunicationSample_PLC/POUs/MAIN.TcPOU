<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.5">
  <POU Name="MAIN" Id="{52a4234c-2a6a-4d4e-a07f-e52b5d3743fc}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	nCycleCounter			: WORD := 0;
	bConnect				: BOOL := FALSE;;
	bRead					: BOOL := FALSE;
	bWrite					: BOOL := FALSE;
	
	nByteRead				: BYTE := 0;
	nByteWrite				: BYTE := 0;
	nDwordRead				: DWORD := 0;
	nDwordWrite				: DWORD := 0;
	
    {attribute 'TcInitSymbol'}
	oidConnectionInstance 	: OTCID; 
	fbConnection		    : FB_S7CommConnection(oidConnectionInstance);
	fbCycleRequest			: FB_S7CommCyclicRequest;
	fbSingleRequestRead		: FB_S7CommSingleRequest;
	fbSingleRequestWrite	: FB_S7CommSingleRequest;
			
    nState                  : INT := 0;
    bReset                  : BOOL := FALSE;
    sErrorTxt				: STRING;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[///////////////////////////////////////////////////////////////
// STATE_MACHINE
//
CASE nState OF

    // RESET
    -2:
		fbSingleRequestRead.bExecute := FALSE;
		fbSingleRequestWrite.bExecute := FALSE;
		fbCycleRequest.bExecute  := FALSE;
		fbConnection.bExecute := FALSE;
		sErrorTxt := '';
		bReset := FALSE;
		nState := 1; // IDLE

    // ERROR
    -1:
	IF bReset THEN
		nState := -2; // RESET
	END_IF

	// INIT
	0:
	
	fbSingleRequestRead.AddReadVar(ADR(nByteRead), SIZEOF(nByteRead), 0, E_S7COMM_DATAAREA.DATA_BLOCKS, 2);
	fbSingleRequestRead.AddReadVar(ADR(nDwordRead), SIZEOF(nDwordRead), 2, E_S7COMM_DATAAREA.DATA_BLOCKS, 2);

	fbSingleRequestWrite.AddWriteVar(ADR(nByteWrite), SIZEOF(nByteWrite), 0, E_S7COMM_DATAAREA.DATA_BLOCKS, 2);
	fbSingleRequestWrite.AddWriteVar(ADR(nDwordWrite), SIZEOF(nDwordWrite), 2, E_S7COMM_DATAAREA.DATA_BLOCKS, 2);
	
	fbCycleRequest.AddReadVar(ADR(nCycleCounter), SIZEOF(nCycleCounter), 256, E_S7COMM_DATAAREA.INPUT, 0);
	
	fbConnection.AddRequest(fbCycleRequest);
	fbConnection.AddRequest(fbSingleRequestRead);
	fbConnection.AddRequest(fbSingleRequestWrite);
    nState := -2; // RESET
	
	// IDLE
	1:
	IF bConnect THEN
		fbConnection.sIpAddr := '192.168.178.100';
		fbConnection.eCpuType := E_S7COMM_CPUTYPE.S7300;
		fbConnection.nRack := 0;
		fbConnection.nSlot := 0;
		
		fbConnection.bExecute := TRUE;
		bConnect := FALSE;
		nState := 10; // WAIT FOR CONNECTION
	END_IF
	
	
	// WAIT FOR CONNECTION
	10:
	IF fbConnection.bIsConnected THEN
		fbCycleRequest.nCycleTimeMs := 100;
		fbCycleRequest.bExecute := TRUE;
		nState := 20; // IS CONNECTED
	END_IF
	
	// IS CONNECTED
	20:
	IF bRead THEN
		fbSingleRequestRead.bExecute := TRUE;
		bRead :=FALSE;
		nState := 30; // WAIT FOR READ
	END_IF
	
	IF bWrite THEN
		fbSingleRequestWrite.bExecute := TRUE;
		bWrite :=FALSE;
		nState := 40; // WAIT FOR WRITE
	END_IF
	
    // WAIT FOR READ
	30:
	IF NOT fbSingleRequestRead.bBusy THEN
		fbSingleRequestRead.bExecute := FALSE;
		nState := 20; // IS CONNECTED
	END_IF
	
	// WAIT FOR WRITE
	40:
	IF NOT fbSingleRequestWrite.bBusy THEN
		fbSingleRequestWrite.bExecute := FALSE;
		nState := 20; // IS CONNECTED
	END_IF
	
END_CASE
 

///////////////////////////////////////////////////////////////
// FB_CALLS
//
fbConnection();
fbCycleRequest();
fbSingleRequestRead();
fbSingleRequestWrite();


///////////////////////////////////////////////////////////////
// ERROR_HANDLING
//
if nState > 0 THEN
	IF fbConnection.bError THEN
		sErrorTxt := CONCAT('CONNECTION: ', fbConnection.sErrorTxt);
		nState := -1;
	ELSIF fbCycleRequest.bError THEN
		sErrorTxt := CONCAT('CYCLE REQUEST: ', fbCycleRequest.sErrorTxt);
		nState := -1;
	ELSIF fbSingleRequestRead.bError THEN
		sErrorTxt := CONCAT('READ REQUEST: ', fbSingleRequestRead.sErrorTxt);
		nState := -1;
	ELSIF fbSingleRequestWrite.bError THEN
		sErrorTxt := CONCAT('WRITE REQUEST: ', fbSingleRequestWrite.sErrorTxt);
		nState := -1;
	END_IF
END_IF]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="350" Count="88" />
      <LineId Id="466" Count="0" />
      <LineId Id="439" Count="6" />
      <LineId Id="465" Count="0" />
      <LineId Id="446" Count="3" />
      <LineId Id="451" Count="13" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>