<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="MAIN" Id="{52a4234c-2a6a-4d4e-a07f-e52b5d3743fc}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	bInitDone			: BOOL:=FALSE;
	
	{attribute 'TcInitSymbol'}
	oidModuleInstance 	: OTCID;      
	fbConnection		: FB_S7CommConnection(oidModuleInstance);
	
	fbRequestRead   	: FB_S7CommSingleRequest;
	fbRequestWrite  	: FB_S7CommSingleRequest;
	fbRequestCyclic 	: FB_S7CommCyclicRequest;
	
	nReadByte			: BYTE;
	nWriteByte			: BYTE;
	
	nCyclicReadBit1		: BOOL;
	nCyclicReadBit2		: BOOL;
	nCyclicReadBit3		: BOOL;
	nCyclicReadBit4		: BOOL;
	nCyclicReadDword	: DWORD;
	nCyclicReadreal1	: REAL;
	nCyclicReadreal2	: REAL;
	nCyclicReadInt		: INT;
	
	bRead				: BOOL := FALSE;
	bWrite				: BOOL := FALSE;
	bReset				: BOOL := FALSE;
	bConnect			: BOOL := FALSE;
	
	connectionError		: STRING;	
	requestError		: STRING;

	nState:	UINT := 0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT bInitDone THEN

	// Read
	fbRequestRead.AddReadVar(ADR(nReadByte),  SIZEOF(nReadByte),  0, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	
	// write
	fbRequestWrite.AddWriteVar(ADR(nWriteByte),  SIZEOF(nWriteByte),  0, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	
	// read cyclic
	fbRequestCyclic.AddReadBit(ADR(nCyclicReadBit1), 1, 0, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadBit(ADR(nCyclicReadBit2), 1, 1, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadBit(ADR(nCyclicReadBit3), 1, 2, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadBit(ADR(nCyclicReadBit4), 1, 3, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadVar(ADR(nCyclicReadDword), SIZEOF(nCyclicReadDword),  2, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadVar(ADR(nCyclicReadreal1), SIZEOF(nCyclicReadreal1),  6, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadVar(ADR(nCyclicReadreal2), SIZEOF(nCyclicReadreal2), 10, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	fbRequestCyclic.AddReadVar(ADR(nCyclicReadInt),   SIZEOF(nCyclicReadInt),   14, S7COMM_DATAAREA_DATA_BLOCKS, 1);
	
	// add to connection
	fbConnection.AddRequest(fbRequestRead);	
	fbConnection.AddRequest(fbRequestWrite);	
	fbConnection.AddRequest(fbRequestCyclic);
	
	// init fbs
	fbConnection.bExecute := FALSE;
	fbRequestWrite.bExecute := FALSE;
	fbRequestRead.bExecute := FALSE;
	fbRequestCyclic.bExecute := FALSE;
	
	// Done
	bInitDone := TRUE;
	nState := 1;
END_IF


CASE nState OF
	// IDLE
	0:;
	
	// CONNECT
	1:
	IF bConnect THEN
		fbConnection.sIpAddr := '10.3.32.101';
		fbConnection.eCpuType := E_S7COMM_CPUTYPE.S71500;
		fbConnection.nRack := 0;
		fbConnection.nSlot := 0;
		
		fbConnection.bExecute := TRUE;
		bConnect := FALSE;
		nState := 10;
	END_IF
	
	// WAIT FOR CONNECT
	10:
	IF NOT fbConnection.bBusy THEN
		IF fbConnection.bError THEN
			connectionError := fbConnection.sErrorTxt;
			nState := 1000;
		ELSE
			fbRequestCyclic.nCycleTimeMs := 1000;
			fbRequestCyclic.bExecute := TRUE;
			nState := 20;
		END_IF
	END_IF
	
	// RUNNING
	20:
	IF bRead THEN
		bRead := FALSE;
		fbRequestRead.bExecute := TRUE;
		nState := 30;
	END_IF
	
	IF bWrite THEN
		bWrite := FALSE;
		fbRequestWrite.bExecute := TRUE;
		nState := 40;
	END_IF
	
	IF fbRequestCyclic.bError THEN
		requestError := fbRequestCyclic.sErrorTxt;
		nState := 1000;
	END_IF
	
	30:
	IF NOT fbRequestRead.bBusy THEN
		IF fbRequestRead.bError THEN
			requestError := fbRequestRead.sErrorTxt;
			nState := 1000;
		ELSE
			fbRequestRead.bExecute := FALSE;
			nState := 20;
		END_IF
	END_IF
	
	40:
	IF NOT fbRequestWrite.bBusy THEN
		IF fbRequestWrite.bError THEN
			requestError := fbRequestWrite.sErrorTxt;
			nState := 1000;
		ELSE
			fbRequestWrite.bExecute := FALSE;
			nState := 20;
		END_IF
	END_IF
	
	// ERROR
	1000:
	IF bReset THEN
		nState := 2000;
	END_IF
	
	// RESET
	2000:
		fbConnection.bExecute := FALSE;
		fbRequestWrite.bExecute := FALSE;
		fbRequestRead.bExecute := FALSE;
		fbRequestCyclic.bExecute := FALSE;
		connectionError := '';
		requestError := '';
		nState := 1;
END_CASE

// fb calls
fbConnection();
fbRequestRead();
fbRequestWrite();
fbRequestCyclic();
]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="64" Count="3" />
      <LineId Id="69" Count="1" />
      <LineId Id="267" Count="0" />
      <LineId Id="72" Count="1" />
      <LineId Id="280" Count="3" />
      <LineId Id="197" Count="0" />
      <LineId Id="284" Count="2" />
      <LineId Id="200" Count="0" />
      <LineId Id="196" Count="0" />
      <LineId Id="78" Count="3" />
      <LineId Id="292" Count="0" />
      <LineId Id="82" Count="4" />
      <LineId Id="291" Count="0" />
      <LineId Id="87" Count="11" />
      <LineId Id="100" Count="3" />
      <LineId Id="110" Count="81" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>